config {
    type: "table",
    name: "ipv4_locations",
    schema: "unified",
    tags: ["unified"],
    assertions: {
        uniqueKey: ["last_modified_date", "network"]
    },
    description: "Dados de geolocalização e rede unificados",
    dependencies: [
      "completness_locations_raw",
      "completness_ipv4_raw",
      "conformity_city_blocks_ipv4",
      "unique_network_ipv4",
      "precision_city_blocks_ipv4_latitude",
      "precision_city_blocks_ipv4_longitude",
      "uniqueness_ipv4",
    ],
    columns: {
        network: "Esta é a rede IPv4 no formato CIDR.",
        octet_1: "Primeiro octeto do da bloco CIDR que define a rede.",
        network_start_ip: "Primeiro endereço IP da rede.",
        network_last_ip: "Último endereço IP válido da rede.",
        network_start_ip_decimal: "Primeiro endereço IP da rede em decimal",
        network_last_ip_decimal: "Último endereço IP da rede em decimal",
        network_size: "Número de endereços que a rede contém.",
        continent_code: "O código do continente para esta localidade.",
        continent_name: "O nome do continente para esta localidade.",
        country_iso_code: "Um código de país ISO 3166-1 de dois caracteres para o país associado ao local.",
        country_name: "O nome do país para este local.",
        city_name: "O nome da cidade para esta localidade.",
        subdivision_1_name: "O nome da subdivisão deste local. Tal como acontece com o código de subdivisão, esta é a subdivisão menos específica para o local.",
        subdivision_2_name: "O nome da subdivisão deste local. Tal como acontece com o código de subdivisão, esta é a subdivisão mais específica para o local.",
        last_modified_date: "Data da última atualização dos dados.",
    },
    bigquery: {
      partitionBy: 'last_modified_date',
      requirePartitionFilter : true,
      partitionExpirationDays: 90,
      clusterBy: ['octet_1']
    }
}

post_operations {
    ALTER TABLE ${self()}
    SET OPTIONS (
        expiration_timestamp = TIMESTAMP_ADD(CURRENT_TIMESTAMP(), INTERVAL 90 DAY)
    );
}

SELECT
  a.network,
  split(a.network, '.')[offset(0)] as octet_1,
  a.network_start_ip,
  a.network_last_ip,
  a.network_start_ip_decimal,
  a.network_last_ip_decimal,
  a.network_size,
  b.continent_code,
  b.continent_name,
  b.country_iso_code,
  b.country_name,
  b.city_name,
  b.subdivision_1_name,
  b.subdivision_2_name,
  a.last_modified_date

FROM
  ${ref('city_blocks_ipv4')} as a
LEFT JOIN
  ${ref('city_locations')} as b
ON
  a.geoname_id = b.geoname_id

WHERE a.last_modified_date = "${dataform.projectConfig.vars.lastModifiedDate}" and
  b.last_modified_date = "${dataform.projectConfig.vars.lastModifiedDate}"
